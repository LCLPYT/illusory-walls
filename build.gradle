import work.lclpnet.build.task.GithubDeploymentTask

plugins {
	id 'fabric-loom' version '1.1.8'
	id 'maven-publish'
	id 'gradle-build-utils' version '1.4.0'
    id 'com.modrinth.minotaur' version '2.4.3'
}

Properties props = buildUtils.loadProperties('publish.properties') as Properties

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = buildUtils.gitVersion()
group = project.maven_group

repositories {
    mavenCentral()

    maven {
        url "https://repo.lclpnet.work/repository/internal"
    }

    exclusiveContent {
        forRepository {
            maven {
                name = "Modrinth"
                url = "https://api.modrinth.com/maven"
            }
        }
        filter {
            includeGroup "maven.modrinth"
        }
    }
}

loom {
    splitEnvironmentSourceSets()

    mods {
        illwalls {
            sourceSet sourceSets.main
            sourceSet sourceSets.client
        }
    }

    accessWidenerPath = file("src/main/resources/illwalls.accesswidener")
}

dependencies {
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "work.lclpnet.mods.kibu:kibu-hook-api:${project.kibu_hook_api_version}"
    modImplementation "work.lclpnet.mods.kibu:kibu-hooks:${project.kibu_hooks_version}"
    modImplementation "work.lclpnet.mods.kibu:kibu-schematic-fabric:${project.kibu_schematic_fabric_version}"
    include "work.lclpnet.mods.kibu:kibu-hook-api:${project.kibu_hook_api_version}"
    include "work.lclpnet.mods.kibu:kibu-hooks:${project.kibu_hooks_version}"
    include "work.lclpnet.mods.kibu:kibu-schematic-fabric:${project.kibu_schematic_fabric_version}"

    if (project.hasProperty('sodium_version')) {
        modImplementation "maven.modrinth:sodium:${project.sodium_version}"
    }

    if (project.hasProperty('iris_version')) {
        modImplementation "maven.modrinth:iris:${project.iris_version}"
        implementation "org.anarres:jcpp:1.4.14"
        implementation "io.github.douira:glsl-transformer:2.0.0-pre12"
        implementation "org.antlr:antlr4-runtime:4.11.1"
    }

    /* other Java dependencies */
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    implementation "work.lclpnet.mods.kibu:kibu-schematic-api:${project.kibu_schematic_api_version}"
    implementation "work.lclpnet.mods.kibu:kibu-nbt-api:${project.kibu_nbt_api_version}"
    include "work.lclpnet.mods.kibu:kibu-schematic-api:${project.kibu_schematic_api_version}"
    include "work.lclpnet.mods.kibu:kibu-nbt-api:${project.kibu_nbt_api_version}"

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-params:5.9.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

test {
    useJUnitPlatform()
}

configurations {
    testCompileClasspath.extendsFrom(minecraftCommonNamed)
    testRuntimeClasspath.extendsFrom(minecraftCommonNamed)
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

def env = System.getenv()

tasks.register("github", GithubDeploymentTask) {
    dependsOn tasks.remapJar

    config {
        token = env.get("GITHUB_TOKEN")
        repository = env.get("GITHUB_REPO")
    }

    release {
        title = "[$project.minecraft_version] Illusory Walls $project.version"
        tag = buildUtils.latestTag()
    }

    assets.add(tasks.remapJar.archiveFile.get())
}

modrinth {
    token = env.get("MODRINTH_TOKEN")

    projectId = "illusory-walls"
    versionName = "[$project.minecraft_version] Illusory Walls $project.version"
    versionType = "release"
    uploadFile = remapJar

    dependencies {
        required.project "fabric-api"
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = project.archives_base_name

            from components.java

            pom {
                name = 'Illusory Walls'
                description = 'A Fabric mod to add illusory walls to Minecraft'
            }
        }
    }

    repositories {
        maven {
            if (Arrays.stream("DEPLOY_URL", "DEPLOY_USER", "DEPLOY_PASSWORD").allMatch(env.&containsKey)) {
                credentials {
                    username env.get("DEPLOY_USER")
                    password env.get("DEPLOY_PASSWORD")
                }
                url env.get("DEPLOY_URL")
            }
            else if (Arrays.stream("mavenHost", "mavenUser", "mavenPassword").allMatch(props.&containsKey)) {
                credentials {
                    username props.getProperty('mavenUser')
                    password props.getProperty('mavenPassword')
                }
                url props.getProperty('mavenHost')
            } else {
                url "file:///${project.projectDir}/repo"
            }
        }
    }
}